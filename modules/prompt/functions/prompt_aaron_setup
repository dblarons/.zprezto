# Used this guy's blog: http://jeromedalbert.com/migrate-from-oh-my-zsh-to-prezto/
# Available colors: black, green, blue, cyan, red, yellow, magenta, white

function prompt_aaron_precmd {
  git-info
}

function prompt_aaron_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_aaron_precmd

  primary_color='blue'

  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{red}❯%F{yellow}❯%F{green}❯%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %F{green}❮%F{yellow}❮%F{red}❮%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ':$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s' \
    'rprompt' '%C%D'

  # Define prompts.
  PROMPT='%F{$primary_color}%c%f${(e)git_info[prompt]}${editor_info[keymap]} '
  RPROMPT=''
}

prompt_aaron_setup "$@"
